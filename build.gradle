buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation fileTree(include: ['*.jar', "*.dylib", "*.so"], dir: 'libs')
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.json', name: 'json', version: '20200518'
    implementation 'io.antelope:java-sdk:0.0.1-SNAPSHOT'
    implementation 'org.jetbrains:annotations:17.0.0'
}

jar {
    dependsOn 'copyNativeLibrary'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    // Copy lib file before jar is created
    from('antelopejavaabieos/build/lib/main/debug') {
        include '**/*.so'
        include '**/*.dylib'
        into('antelopejavaabieos/build/lib/main/debug')
    }
}

tasks.register('copyNativeLibrary', Copy) {
    dependsOn ':antelopejavaabieos:linkDebug'
    // Copy lib to /resources so tests can run within project (outside of gradle task)
    from('antelopejavaabieos/build/lib/main/debug') {
        include '**/*.so'
        include '**/*.dylib'
    }
    into 'src/main/resources/antelopejavaabieos/build/lib/main/debug'
}

test {
    dependsOn 'copyNativeLibrary'
    systemProperty "java.library.path", file("${project(":antelopejavaabieos").buildDir}/lib/main/debug").absolutePath
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('gitSubmodulesUpdate') {
    exec {
        commandLine 'git', 'submodule', 'update', '--init', '--recursive'
    }
}

tasks.register('deleteCopiedNativeLibrary', Delete) {
    dependsOn('gitSubmodulesUpdate')
    delete fileTree(include: ["*.dylib", "*.so"], dir: 'src/main/resources/antelopejavaabieos/build/lib/main/debug')
}

clean.dependsOn 'deleteCopiedNativeLibrary'

def libraryGroupId = 'io.antelope'
def libraryArtifactId = 'java-abieos-serialization-provider'
def libraryVersion = '0.0.1-SNAPSHOT'

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            pom {
                name = 'Antelope SDK for Java: ABIEOS Serialization Provider'
                description = 'ABIEOS Serialization Provider is intended to be used in conjunction with Antelope SDK for Java as a provider plugin.'
                url = 'https://github.com/mcicu/antelope-java-abieos-serialization-provider/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/mcicu/antelope-java-abieos-serialization-provider/blob/master/LICENSE'
                    }
                }
            }
        }
    }
}
